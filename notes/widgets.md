Widgets
=======

In addition to a `factory`, ComponentWidgets should have a `comparator` method to check if a widget is invalid. This will be used inside the `canBeUpdated` method.

```haxe
package blok.ui;

import blok.core.UniqueId;

using blok.core.ObjectTools;

class ComponentWidget<Props:{}> extends Widget {
  public final props:Props;
  final type:UniqueId;
  final comparator:(
    widget:ComponentWidget<Props>, 
    newWidget:ComponentWidget<Props>
  )->Bool;
  final factory:(widget:ComponentWidget<Props>)->Component;

  public function new(type, props, comparator, factory, key) {
    super(key);
    this.props = props;
    this.comparator = comparator;
    this.type = type;
    this.factory = factory;
  }

  public function getWidgetType():UniqueId {
    return type;
  }

  public function createElement():Element {
    return factory(this);
  }

  public function withProperties(props:Props) {
    return new ComponentWidget(
      type,
      this.props.merge(props),
      comparator,
      factory,
      key
    );
  }

  override function canBeUpdated(newWidget:Widget):Bool {
    if (super.canBeUpdated(newWidget)) {
      return comparator(this, newWidget);
    }
    return false;
  }
}
```

When we're invalidating a widget, especially from an `@update` method, we'll need to create a new widget to compare against, which is what `withProperties` is for. An update method might look something like:

```haxe
public function updateWidgetAndInvalidateElement(props:Dynamic) {
  var newWidget = widget.withProperties(props);
  if (widget.canBeUpdated(newWidget)) {
    widget = newWidget;
    invalidateElement();
  }
}
```

Note that the `comparator` will be generated by the Component build macro, and will basically replace the current property-update system we have.